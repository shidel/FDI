@ECHO OFF
REM - Updated AUTOEXEC file, for DOSBox Hybrid install

REM FreeDOS specific $AUTOEXEC$ file for DOSBox in a Hybrid install. A Hybrid install
REM uses the FreeDOS command line interpreter and the DOSBox kernel. This permits
REM direct access to the HOST directories mounted drives in DOSBox without the need
REM for networking or shutting down the DOS instance. You may wish to have your
REM Z:\$AUTOEXEC$ file check for and automatically launch this batch file
REM at DOSBox startup. Please note that many very low level programs that talk directly
REM to hard disks (like defrag) will not function using a Hybrid Install. 

REM - Load the command line interpreter or set up the DOS environment.
if "%1" == "$OS_NAME$" goto ShellLoaded
C:
CD\
if not exist $FTARGET$\bin\command.com goto END
$FTARGET$\bin\command.com /e:2048 /k %0 $OS_NAME$
goto END

:ShellLoaded

REM - set basic environment

set DOSDRV=$FDRIVE$
set DOSDIR=$FTARGET$

REM - set the executable search path
set OLDPATH=%PATH%
path %DOSDIR%\BIN
if not exist %DOSDIR%\LINKS\NUL goto NOLINKS
path %PATH%;%DOSDIR%\LINKS
:NOLINKS
if "%OLDPATH%" == "" goto NOOLDPATH
path %PATH%;%OLDPATH%
:NOOLDPATH
set OLDPATH=

set NLSPATH=%DOSDIR%\NLS
set HELPPATH=%DOSDIR%\HELP

set TEMP=%DOSDIR%\TEMP
set TMP=%TEMP%
if not exist %TEMP%\NUL mkdir %TEMP%

set LANG=$FLANG$
set TZ=$TZ$

REM - Internationalization settings

REM nlsfunc %DOSDIR%\BIN\COUNTRY.SYS
REM display CON=(EGA,858,2)
REM mode CON CP PREP=((858) %DOSDIR%\CPI\EGA.CPX)
REM keyb US,858,%DOSDIR%\BIN\KEYBOARD.SYS
REM chcp 858
REM mkeyb UK

$LANG_SET$
 
set OS_NAME=$OS_NAME$
set OS_VERSION=$OS_VERSION$

REM - cfgfile and autofile might be deprecated in future
REM - At boot, FreeDOS can use CONFIG.SYS, but it prefers FDCONFIG.SYS

set CFGFILE=%DOSDRV%\CONFIG.SYS
if exist %DOSDRV%\FDCONFIG.SYS set CFGFILE=%DOSDRV%\FDCONFIG.SYS
set AUTOFILE=%0
alias cfg=edit %CFGFILE%
alias auto=edit %AUTOFILE%

REM - Neither APM or MOUSE drivers are required
REM lh fdapm APMDOS
REM lh ctmouse
REM lh share

if not exist %DOSDIR%\BIN\FDNET.BAT goto NONET
call %DOSDIR%\BIN\FDNET.BAT
REM - if errorlevel 1 goto NONET
REM - Add other stuff here once FDNET is loaded..
:NONET

REM - load other configs using external BAT files

if not exist %DOSDIR%\BIN\FDASSIST.BAT goto NOASSIST
call %DOSDIR%\BIN\FDASSIST.BAT
:NOASSIST

REM - additional customization and local settings

set BLASTER=A220 I5 D1 H5 P330
set DIRCMD=/O:GNE /Y
set COPYCMD=/-Y

alias reset=fdisk /reboot
alias reboot=fdapm warmboot
alias halt=fdapm poweroff
alias shutdown=fdapm poweroff

MEM /C /N

REM - Display "Welcome to FreeDOS"
if not exist %DOSDIR%\BIN\WELCOME.BAT goto NOWELCOME
call %DOSDIR%\BIN\WELCOME.BAT
:NOWELCOME

:END
